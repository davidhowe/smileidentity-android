plugins {
    id "com.android.library"
    id "org.jetbrains.kotlin.android"
    id "org.jlleitschuh.gradle.ktlint"
}

android {
    namespace "com.smileidentity.ui"
    resourcePrefix "si_"
    compileSdk 33

    defaultConfig {
        minSdk 23
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        all {
            def sentryDsn = project.hasProperty("SENTRY_DSN") ? "\"${SENTRY_DSN}\"" : "\"\""
            buildConfigField "String", "SENTRY_DSN", sentryDsn
            buildConfigField "String", "VERSION_NAME", "\"${project.version}\""
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion "1.3.2"
    }
}

dependencies {
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "androidx.appcompat:appcompat:1.5.1"
    implementation "com.google.android.material:material:1.7.0"

    implementation "com.jakewharton.timber:timber:5.0.1" // Logging
    implementation "io.sentry:sentry:6.9.2" // Crash Reporting
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'

    // Jetpack Compose version is defined by BOM ("Bill-of-Materials")
    // Latest BOM version: https://developer.android.com/jetpack/compose/setup#bom-version-mapping
    def composeBom = platform("androidx.compose:compose-bom:2022.12.00")
    implementation composeBom

    implementation "androidx.compose.material3:material3"
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-tooling-preview" // Android Studio Preview support
    debugImplementation "androidx.compose.ui:ui-tooling" // Android Studio Preview support
    debugImplementation "androidx.compose.ui:ui-test-manifest" // UI Tests

    implementation "io.github.ujizin:camposer:0.1.0" // CameraX Compose
    implementation "androidx.camera:camera-camera2"

    implementation "com.google.accompanist:accompanist-permissions:0.28.0" // Permissions Compose

    testImplementation "junit:junit:4.13.2"

    androidTestImplementation "androidx.test.ext:junit:1.1.4"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.0"
}
